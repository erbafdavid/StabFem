/// 
/// file Macros_StabFem.edp
///
/// This file contains the case-dependent Macros for boundary conditions and postprocessing output.
///
/// This file can be customized according to requirements but it must contain the following macros :
///
/// boundary conditions : 
/// macro BoundaryconditionsBaseFlow(du,u,v) 
/// macro BoundaryconditionsStability(u,v,symmetry)
///
///
/// For post-processsing purposes :
/// macro SFWriteBaseFlow(filename,ux,uy,p,type,iter)
/// macro SFWriteMode(namefile,ux,uy,p,ev,shift,type,iter)
/// 
/// The following case is for the 2D cylinder and include the additional macros
/// required to compute lift and drag forces : Trainee(ux,uy,p,symmetry) and Portance(ux,uy,p,symmetry)

IFMACRO(!problemtype)
macro problemtype "2D"//EOM
ENDIFMACRO

IFMACRO(!NbComponentsMode)
macro NbComponentsMode 3//EOM
ENDIFMACRO



string ffdatadir = "./WORK/";

string symmetryBaseFlow = "S"; // symmetry propery of base flow :; set "S" if domain is hzalf domain and "N" if it is full domain
int bclat=4,bcinlet=1,bcoutflow=3,bcwall=2,bcaxis=6;




macro BoundaryconditionsBaseFlow(du,u,v)
	           on(bcinlet,du#x=1.5*(1-4*y^2)-u#x,du#y=0.0)
             + on(2,21,22,23,du#x=0.0,du#y=0.0)
             + int1d(th,bcaxis)(du#x*v#x*1e30*(symmetryBaseFlow=="A")+du#y*v#y*1e30*(symmetryBaseFlow=="S"))       
//EOM

macro BoundaryconditionsStability(u,v,symmetry)
	           on(1,u#x=0.0,u#y=0.0)
             + on(2,21,22,23,u#x=0.0,u#y=0.0)
             + int1d(th,6)(u#x*v#x*1e30*(symmetry=="A")+u#y*v#y*1e30*(symmetry=="S")) 
 	   IFMACRO(NbComponentsMode,3)
	          + on(1,u#z=0)
	          + on(2,21,22,23,u#z=0.0)
	          + int1d(th,6)(u#z*v#z*1e30*(symmetry=="A")) 
	   ENDIFMACRO
	          
	          /*ENDIFMACRO*/      
//EOM



// HERE ARE SOME MACROS WHICH ARE USEFUL FOR POST-PROCESSING WITH A 2D FLOW OVER A BLUFF BODY

macro Trainee(u,symmetry)   
	(
	(1*(symmetry=="N")+2*(symmetry=="S"))
	*(
	int1d(th,21,22,23)(u#p*N.x)
	-nu*int1d(th,21,22,23)( (2*dx(u#x)*N.x+(dx(u#y)+dy(u#x))*N.y))
	)
	)
//EOM
	

macro Portance(u,symmetry)   
	(
	(1*(symmetry=="N")+2*(symmetry=="A"))
	*(
	int1d(th,21,22,23)(u#p*N.y)
	-nu*int1d(th,21,22,23)( (2*dy(u#y)*N.y+(dx(u#y)+dy(u#x))*N.x))
	)
	)
//EOM



macro SFWriteMesh(filename,TH)
{
     ofstream fileFF(filename);
     fileFF << "### MESH generated by Freefem++ ; " << endl;
     fileFF << "2D mesh" << endl;
     fileFF << "Format :" << endl;
	 fileFF << "int np int Ndof real deltamin real deltamax" ;
	 string PostProcessMeshDescription = " " ; /*description of customizable part*/
	 fileFF << PostProcessMeshDescription << endl << endl ;
     fespace femp1N(TH,P1);
     fespace femp2xfemp2xfemp1N(TH,[P2,P2,P1]);	  
     femp2xfemp2xfemp1N [uNx,uNy,uNp];
	 fileFF << TH.nv <<  endl << uNx[].n << endl ;
 	 cout << "#### Mesh information :" << endl;  
     cout << "## Number of vertices    " << TH.nv <<  endl;
	 cout << "## Number of DOF : " << uNx[].n << endl << endl;  
 	 femp1N delta = hTriangle;
	cout << "## delta min : " << delta[].min << endl;
	cout << "## delta max : " << delta[].max << endl;
	fileFF << delta[].min << endl;
	fileFF << delta[].max << endl;   
    /*Next is customizable part ; here determination of the grid size at several points */ 
}; 	 
//EOM




macro SFWriteBaseFlow(filename,u,typeFlow,iter)
	if(iter>=0)
	{
	fespace p1forff2m(th,P1);  
	p1forff2m xx,yy;
	xx=x;
	yy=y;
    p1forff2m ut,vt,pt,vort;
	ut=u#x;
	vt=u#y;
	pt=u#p;
	vort=dy(u#x)-dx(u#y); /* vorticity */
	p1forff2m psi,phi;
/*	solve Laplace(psi,phi, solver = CG) */
/* 	= int2d(th)(dx(psi)*dx(phi)+dy(psi)*dy(phi)) */
/* 	- int2d(th)(-vort*phi) */
/* 	- int1d(th,bcinlet,bcoutflow,bclat,bcwall)((-N.x*ux+N.y*uy)*phi); */   
	ofstream file(filename);
   	{
	file << "### Data generated by Freefem++ ; " << endl;
    file << typeFlow << endl;
    file << "Format : " << endl;	
	file << "real Re P1 ux P1 uy P1 p P1 vort P1 psi real iter " ;
	string PostProcessBaseFlowDescription = " " ;
	file << PostProcessBaseFlowDescription << endl << endl ; 
	file << Re << endl;	
			for (int j=0;j<ut[].n ; j++) file << ut[][j] << endl;
			for (int j=0;j<ut[].n ; j++) file << vt[][j] << endl;
			for (int j=0;j<ut[].n ; j++) file << pt[][j] << endl;
			for (int j=0;j<ut[].n ; j++) file << vort[][j] << endl;
			for (int j=0;j<ut[].n ; j++) file << psi[][j] << endl;
			file << iter << endl;
	
	cout << "#### customisable output for IMPACTING JET (2D) : NOTHING IMPLEMENTED " << endl; 
   
   	}
	}
	else
	{
	ofstream file(filename);
	file << "### Data generated by Freefem++ ; " << endl;
    file << "BaseFlow NOT CONVERGED" << endl;
    file << "Format : " << endl;	
	file << "int iter " << endl;
	file << -1 << endl;
	};
//EOM
	
	
macro SFWriteMode(namefile,u,k,ev,shift,typeFlow,iter)
/*macro SFWriteMode(namefile,u,ev,shift,typeFlow,iter)
real k=0;*/
		 {
		 ofstream file(namefile);
   		 fespace p1forff2m(th,P1); 
		 p1forff2m xx,yy;
		 xx=x;
		 yy=y;
    	 p1forff2m<complex> ux1,uy1,pp1,vort1;  		 
 		 file << "### Data generated by Freefem++ ; " << endl;
    	 file << typeFlow << endl;
    	 file << "Format :" << endl;
    	 string descriptionFF;
    	 if(typeFlow=="EigenModeD"||typeFlow=="HarmonicMode")
    	 	{  descriptionFF="real Re real k complex lambda complex shift int iter P1c ux1 P1c uy1 P1c p1 P1c vort1 real AEnergy"; }
    	 else 
    	    {  descriptionFF="real Re  real k complex lambda complex shift int iter P1c ux1Adj P1c uy1Adj P1c p1Adj P1c vort1Adj real AEnergyAdj"; }
	     file << descriptionFF << endl << endl ; 
		 file << Re  << endl << k << endl << real(ev) << " " << imag(ev) << " " << real(shift) << " " << imag(shift) << " " << iter << endl << endl;
 		ux1=u#x;
		uy1=u#y;
IFMACRO(NbComponentsMode,3)
		p1forff2m<complex> uz1;
		uz1=u#z;
ENDIFMACRO
		pp1=u#p;
		vort1=dy(u#x)-dx(u#y);
		
		for (int j=0;j<ux1[].n ; j++) file << real(ux1[][j]) << " " << imag(ux1[][j]) << endl;
		for (int j=0;j<ux1[].n ; j++) file << real(uy1[][j]) << " " << imag(uy1[][j]) << endl;
IFMACRO(NbComponentsMode,3)
		for (int j=0;j<ux1[].n ; j++) file << real(uy1[][j]) << " " << imag(uy1[][j]) << endl;
ENDIFMACRO
		for (int j=0;j<ux1[].n ; j++) file << real(pp1[][j]) << " " << imag(pp1[][j]) << endl;
		for (int j=0;j<ux1[].n ; j++) file << real(vort1[][j]) << " " << imag(vort1[][j]) << endl;
		
		cout << "#### customisable output for a mode in the WAKE OF A 2D BLUFF BODY : " << endl; 

	
		real AEnergyMode = sqrt(2*int2d(th)(abs(u#x)^2+abs(u#y)^2)); 
		cout << "#   Amplitude (with definition of Mantic-Lugo) = " << AEnergyMode << endl;
		file << AEnergyMode << endl;

		/*complex LiftMode = Portance(u,"A"); 
		cout << "#   Cy = " << 2*LiftMode << endl;
		file << 2*real(LiftMode) << " " << 2*imag(LiftMode) << endl;
		*/
		
		};
//EOM	




//macro PostProcessBaseFlowOutput(file,u1,u2,p)
	