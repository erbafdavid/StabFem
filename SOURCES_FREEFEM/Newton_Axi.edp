//
//  PROGRAM Newton_2D.edp
//  computation of 2D base flow [ux,uy](x,y)
//   
//
//  INPUT PARAMETERS (from keyboard or pipe) :
//   	Re
//	INPUT FILES :
//		BaseFlow_guess.txt	-> guess base flow (generated by mesh generator, time integration or previous Newton)
// 		mesh.msh 			->  mesh in Freefem format
//  OUTPUT FILES :
//		BaseFlow.txt			-> base flow (FF format)
//      BaseFlow.ff2m         -> base flow (FF2M format) 
//
// 	ERROR MANAGEMENT : 
//      if Newton iteration diverges, BaseFlow.txt is not generated and "iter" parameter in BaseFlow.ff2m is -1.
//
//
// INCORPORATED INTO THE STABFEM PROJECT BY D. FABRE ON A BUSY DAY AT UNIVERSITA DI SALERNO, 18 july 2017
// REwriten on october 2 2017 with macros.

cout << "$$$$ ENTERING Newton_Axi.edp " << endl << "$$" << endl; 


		include "SF_Geom.edp";
		include "Macros_StabFem.edp";
		
		if(ncomponentsBF==3) 
		{ cout << "$$ERROR in Newton_Axi :  for this solver the parameter ncomponentsBF should be 2 not 3 !" << endl;
		  cout << "$$Change this in Macro_StabFem.edm" < endl;
		  exit(-1);
		 } 
		
		
	     verbosity = 0;
		mesh th=readmesh(ffdatadir+"mesh.msh");
		
        fespace Xh(th,P2);       //f.e. space for veloecity components
		fespace Mh(th,P1);      //f.e space for pressure
		fespace XXMh(th,[P2,P2,P1]); // f.e. space for triplet [u,v,p]
		fespace XXXMh(th,[P2,P2,P2,P1]); // f.e. space for triplet [u,v,p]


		XXMh [ux,ur,up];          ///////  -> unknown for the Navier-Stokes problem
		XXMh [vx,vr,vp];          ///////  -> test function for Navier-Stokes
		XXMh [upx,upr,upp];   	 ///////  -> buffer vector  to store previous iteration (Newton) 
		XXMh [dux,dur,dup]; 		 ///////  -> increment for the Newton iteration
		
		
	real Re;
	real Reans,lecture;

	cout << "$$ Entrer le Nombre de Reynolds :" << endl;
    cin >>  Re;
		XXXMh [u3cx,u3cr,u3cphi,u3cp];
		int NdimUVP = ux[].n;
    	int NdimUVWP = u3cx[].n;
    	int NdimBaseFlow;
    	{
          ifstream file(ffdatadir+"BaseFlow_guess.txt");
          file >> NdimBaseFlow ;
    	}
		
		if(NdimBaseFlow == NdimUVP) 
		{
        cout << "$$Ndim = " << NdimUVP << " : reading UVP baseflow " << endl;
        ifstream file(ffdatadir+"BaseFlow_guess.txt");
        file >> ux[];
		file >> Reans;
        }
        else if(NdimBaseFlow == NdimUVWP) 
        {
        cout << "$$Ndim = " << NdimUVWP << " : reading UVWP baseflow ; skipping W component" << endl;
        ifstream file(ffdatadir+"BaseFlow_guess.txt");
        file >> u3cx[];
        [ux,ur,up] = [u3cx,u3cr,u3cp]; 
		file >> Reans;
        }
        else 
        {
        cout << "$$ Error : Wrong dimension For BaseFlow_guess.txt file" << endl;
        exit(-1);
        };


	//	[ux,ur,up] = [ur,ux,up];

	cout << "$$ Newton pour Re = " << Re << endl;	
	cout << "$$ CI : " << Reans << endl; 
	real nu=1./Re;
	real eps = 1e-20; // may not be required


/////////////////////////////////////////////////////////////////////////////////
                         /////// macro to define some useful operators

macro div(u) (dx(u#x)+dy(u#r)+u#r/y)// macro for divergence 
macro Grad(u) [[dx(u#x), dy(u#x),  u#x-u#x ], [dx(u#r), dy(u#r), u#x-u#x],[u#x-u#x,u#x-u#x,u#r/y ]] // macro for rate-of-deformation tensor
macro D(u) [[dx(u#x), .5*(dx(u#r)+dy(u#x)),  u#x-u#x ], [.5*(dx(u#r)+dy(u#x)), dy(u#r),u#x-u#x],[u#x-u#x,u#x-u#x,u#r/y ]] // macro for rate-of-deformation tensor

macro Conv(ua,ub,v) ( 
              ((ua#x*dx(ub#x)+ua#r*dy(ub#x))+(ub#x*dx(ua#x)+ub#r*dy(ua#x)))*v#x 
            + ((ua#x*dx(ub#r)+ua#r*dy(ub#r))+(ub#x*dx(ua#r)+ub#r*dy(ua#r)))*v#r 
                    ) // macro for mutual convection operator

//macro Conv(ua,ub,v) ( 
//             (Grad(ua)*[ub#x,ub#r,ub#x-ub#x] +Grad(ub)*[ua#x,ua#r,ua#x-ua#x])*[v#x,v#r,v#x-v#x]'
//                    ) // macro for mutual convection operator






//macro Diffusion(nu,u1,u2,v1,v2)  (-nu*( dx(u1)*dx(v1)+dy(u1)*dy(v1)+dx(u2)*dx(v2)+dy(u2)*dy(v2))) // integration by parts of (v * Delta (u) )  


//////////////////////////////////////////////////////////////////////////////////
                         /////// linearized system
problem NewtonIter([dux,dur,dup],[vx,vr,vp]) =
    int2d(th)( 
			(
			-2*nu*(D(du):D(v))
             + vp*div(du)
             + div(v)*dup
				-Conv(up,du,v)
		     )*y
              )
             +
    int2d(th)(
		  (
    		-2*nu*(D(up):D(v))
             + vp*div(up)
             + div(v)*upp
             -.5*Conv(up,up,v)
          )*y
              )
	+  BoundaryconditionsBaseFlow(du,up,v);     
    
    
// parameters for Newton loop            
    real tolerance=2e-12;
    int Nitermax=20;
    real ener=10;	
    int iter=0;
    
// Newton loop
        while((ener>tolerance)&(ener<50)&(iter<=Nitermax))
        {
	    upx[]=ux[];        ////// Uold=U
	    
	    
	    
        NewtonIter ;           //// update of current solution u1=u1-w
        
        
        plot(dux,fill=1,wait=0,value=1,cmm="dux");
        
        ux[] = ux[]+dux[];
		real enerl2=dux[].l2;
		ener=sqrt(int2d(th)(dux^2+dur^2)/th.area);
	    cout << "$$ iter = "<< iter+1 << "   Re = " << Re << " res = " << ener << " ; res l2 = " << enerl2 << endl;
        iter++;
	  } ;



// sortie des résultats 

		if((ener>50)||(iter>Nitermax))
		{
			cout << "$$ NEWTON ITERATION FAILED" << endl;
			exec("rm "+ffdatadir+"BaseFlow.txt");
			SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",u,"BaseFlow",-1);
		}
		else
		
		{
		  {ofstream file(ffdatadir+"BaseFlow.txt"); // file in .txt format for FreeFem solvers
		  file << ux[]; 
		  file << endl << endl << Re;
		  }; 
          exec("cp "+ffdatadir+"BaseFlow.txt "+ffdatadir+"BaseFlow_guess.txt"); 
          SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",u,"BaseFlow",iter); // file in .ff2ml format for StabFem driver
          // plot 
		  plot(ux,fill=1,wait=0,value=1,cmm="final U1");
			//plot(uy,fill=1,wait=0,value=1,cmm="final U2");
			//plot(p,fill=1,wait=0,value=1,cmm="final Pressure");
    
		};
          
cout << "$$$$ SUCESSFULLY LEAVING Newton_Axi.edp " << endl ; 
