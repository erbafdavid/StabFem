//
//  PROGRAM Newton_2D.edp
//  computation of 2D base flow [ux,uy](x,y)
//   
//
//  INPUT PARAMETERS (from keyboard or pipe) :
//   	Re
//	INPUT FILES :
//		BaseFlow_guess.txt	-> guess base flow (generated by mesh generator, time integration or previous Newton)
// 		mesh.msh 			->  mesh in Freefem format
//  OUTPUT FILES :
//		BaseFlow.txt			-> base flow (FF format)
//      BaseFlow.ff2m         -> base flow (FF2M format) 
//
// 	ERROR MANAGEMENT : 
//      if Newton iteration diverges, BaseFlow.txt is not generated and "iter" parameter in BaseFlow.ff2m is -1.
//
//
// INCORPORATED INTO THE STABFEM PROJECT BY D. FABRE ON A BUSY DAY AT UNIVERSITA DI SALERNO, 18 july 2017
// REwriten on october 2 2017 with macros.


		include "Macros_StabFem.edp";
		
	     verbosity = 0;
		mesh th=readmesh(ffdatadir+"mesh.msh");
		
        fespace Xh(th,P2);       //f.e. space for veloecity components
		fespace Mh(th,P1);      //f.e space for pressure
		fespace XXMh(th,[P2,P2,P1]); // f.e. space for triplet [u,v,p]

		XXMh [ux,uy,p];          ///////  -> unknown for the Navier-Stokes problem
		XXMh [vx,vy,q];          ///////  -> test function for Navier-Stokes
		XXMh [upx,upy,pp];   	 ///////  -> buffer vector  to store previous iteration (Newton) 
		XXMh [dux,duy,dp]; 		 ///////  -> increment for the Newton iteration
	real Re;
	real Reans,lecture;

	cout << " Entrer le Nombre de Reynolds :" << endl;
    cin >>  Re;

		{
          ifstream file(ffdatadir+"BaseFlow_guess.txt");
        file >> ux[];
		file >> Reans;
        };

	cout << " Newton pour Re = " << Re << endl;	
	cout << " CI : " << Reans << endl; 
	real nu=1./Re;
	real eps = 1e-20; // may not be required


/////////////////////////////////////////////////////////////////////////////////
                         /////// macro to define some useful operators

macro div(u) (dx(u#x)+dy(u#y))// macro for divergence 
macro Conv(ua,ub,v) ( 
              ((ua#x*dx(ub#x)+ua#y*dy(ub#x))+(ub#x*dx(ua#x)+ub#y*dy(ua#x)))*v#x 
            + ((ua#x*dx(ub#y)+ua#y*dy(ub#y))+(ub#x*dx(ua#y)+ub#y*dy(ua#y)))*v#y 
                    ) // macro for mutual convection operator
macro D(u) [[dx(u#x), .5*(dx(u#y)+dy(u#x))], [.5*(dx(u#y)+dy(u#x)), dy(u#y)]] // macro for rate-of-deformation tensor

macro Diffusion(nu,u1,u2,v1,v2)  (-nu*( dx(u1)*dx(v1)+dy(u1)*dy(v1)+dx(u2)*dx(v2)+dy(u2)*dy(v2))) // integration by parts of (v * Delta (u) )  


//////////////////////////////////////////////////////////////////////////////////
                         /////// linearized system
problem NewtonIter([dux,duy,dp],[vx,vy,q]) =
    int2d(th)( 
//    		Diffusion(nu,dux,duy,vx,vy)
			-2*nu*(D(du):D(v))
             + q*div(du)
             + div(v)*dp
				-Conv(up,du,v)
             )
             +
    int2d(th)(
//        		Diffusion(nu,upx,upy,vx,vy)
    		-2*nu*(D(up):D(v))
             + q*div(up)
             + div(v)*pp
             -.5*Conv(up,up,v)
             )
	+  BoundaryconditionsBaseFlow(du,up,v);     
    
    
// parameters for Newton loop            
    real tolerance=2e-12;
    int Nitermax=20;
    real ener=10;	
    int iter=0;
    
// Newton loop
        while((ener>tolerance)&(ener<50)&(iter<=Nitermax))
        {
	    upx[]=ux[];        ////// Uold=U
        NewtonIter ;           //// update of current solution u1=u1-w
        ux[] = ux[]+dux[];
		real enerl2=dux[].l2;
		ener=sqrt(int2d(th)(dux^2+duy^2)/th.area);
	    cout << " iter = "<< iter+1 << "   Re = " << Re << " res = " << ener << " ; res l2 = " << enerl2 << endl;
        iter++;
	  } ;



// sortie des résultats 

		if((ener>50)||(iter>Nitermax))
		{
			cout << " NEWTON ITERATION FAILED" << endl;
			exec("rm "+ffdatadir+"BaseFlow.txt");
			SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",ux,uy,p,"BaseFlow",-1);
		}
		else
		
		{
		  {ofstream file(ffdatadir+"BaseFlow.txt"); // file in .txt format for FreeFem solvers
		  file << ux[]; 
		  file << endl << endl << Re;
		  }; 
          exec("cp "+ffdatadir+"BaseFlow.txt "+ffdatadir+"BaseFlow_guess.txt"); 
          SFWriteBaseFlow(ffdatadir+"BaseFlow.ff2m",ux,uy,p,"BaseFlow",iter); // file in .ff2ml format for StabFem driver
          // plot 
			plot(ux,fill=1,wait=0,value=1,cmm="final U1");
			plot(uy,fill=1,wait=0,value=1,cmm="final U2");
			plot(p,fill=1,wait=0,value=1,cmm="final Pressure");
    
		};
          