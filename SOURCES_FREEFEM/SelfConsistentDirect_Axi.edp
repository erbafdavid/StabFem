//
//  PROGRAM SelfConsistentDirect_Axi.edp
//	
//	Solves directly the Self-Consistent equations to decribe the limit cycle with a mean flow and a quasi-linear mode (Mantic-lugo et al)
//
// This program is adapted from SelfConsistentDirect_2D.edp and has only been validated for a birdcall whistle !
//   
//  The flow is assumed as an expansion in the form : 	u = u_0 + [ u_{1,c} cos (omega t) + u_{1,s} sin (omega t) ] e^{lambda t}
//  This expansion is equivalent to Mantic-Lugo et al : u = u_0 + A [U_1 e^{i omega t + i sigma t + cc] where u_1 is the complex self-consistent mode.
// 	The connection between both parametrizations is u_{1,c} - i u_{1,s} = 2 A u_1 where u_1 is normalised with ||u_1|| = 1/sqrt(2). 
//
//  INPUT PARAMETERS (from keyboard or pipe) :
//   	Re , omega_guess, sigma, typenormalization [,norm] 
//
//       the "sigma" parameter is the amplification rate in the SC model of Mantic-Lugo ; if zero it reduces to a simple truncated fourier expansion	
//
//	INPUT FILES :
//		MeanFlow_guess.txt				-> starting field for mean-flow (real) 
//      SelfConsistentMode_guess.txt 	-> starting field for self-consistent mode (stored as COMPLEX ; real and imaginary parts are u1,c and -u1,s)
// 		mesh.msh 						->  mesh in Freefem format
//  OUTPUT FILES :
//		MeanFlow.txt					-> Converged mean flow (real)
//		MeanFlow.ff2m					
//      SelfConsistentMode.txt			-> Converged self-consistent mode (stored as COMPLEX with same convention) 
//      SelfConsistentMode.ff2m 
//
// 	ERROR MANAGEMENT : 
//      if Newton iteration diverges, SelfConsistentMode.txt is not generated and "iter" parameter in Eigenmode.ff2m is -1.
//




//load "MUMPS_seq"
include "SF_Geom.edp";
include "Macros_StabFem.edp";
string copyfile = "yes"; // set to "yes" to copy the output files into the "guess". Better to disable when using in Matlbab driver mode.


/////////////////////////////////////////////////////////////////////////////////


mesh th=readmesh(ffdatadir+"mesh.msh");                    ///// Read mesh
//int bclat=4,bcinlet=1,bcoutflow=3,bcwall=2,bcaxis=6;

/////////////////////////////////////////////////////////////////////////////////

fespace Xh(th,P2);             ////// f.e. space for vel.
fespace Mh(th,P1);            ////// f.e. space for pressure
fespace XXMh(th,[P2,P2,P1]); ////// f.e. space for triplet [u,v,p]
fespace XXXMh(th,[P2,P2,P2,P1]); ////// f.e. space for triplet [u,v,w,p]
fespace BIGSPACE(th,[P2,P2,P1,P2,P2,P1,P2,P2,P1]);

XXMh [ubx,ubr,ubp]; // base flow (start) 
XXXMh<complex> [u1x,u1r,u1phi,u1p];    //eigenmode (start)      
Mh dumb; // to construct the rhs's


XXMh [u0x,u0r,u0p],[u1cx,u1cr,u1cp],[u1sx,u1sr,u1sp]; // components 0 (mean flow) and 1 (fundamental, re and im) of HB  
XXMh [du0x,du0r,du0p],[du1cx,du1cr,du1cp],[du1sx,du1sr,du1sp]; // Increments in Newton
XXMh [vx,vr,vp]; // test functions


real Re, ReM;
real omega, omegaM, sigmaM, domega;
     
////////////////////////////////////////////////////////////////////////////////


// Macro operators for 2D-Axi 

macro div(u) (dx(u#x)+dy(u#r)+u#r/y)// macro for divergence 

macro Grad(u) [[dx(u#x), dy(u#x),  u#x-u#x ], [dx(u#r), dy(u#r), u#x-u#x],[u#x-u#x,u#x-u#x,u#r/y ]] // macro for rate-of-deformation tensor

macro D(u) [[dx(u#x), .5*(dx(u#r)+dy(u#x)),  u#x-u#x ], [.5*(dx(u#r)+dy(u#x)), dy(u#r),u#x-u#x],[u#x-u#x,u#x-u#x,u#r/y ]] // macro for rate-of-deformation tensor

macro Conv(ua,ub,v) ( 
              ((ua#x*dx(ub#x)+ua#r*dy(ub#x))+(ub#x*dx(ua#x)+ub#r*dy(ua#x)))*v#x 
            + ((ua#x*dx(ub#r)+ua#r*dy(ub#r))+(ub#x*dx(ua#r)+ub#r*dy(ua#r)))*v#r 
                    )
//EOM


macro NS(nu,u,v) 
		(
		 -2*nu*(D(u):D(v))
        + u#p*div(v)
        + v#p*div(u)
        - .5*Conv(u,u,v)
        )
//EOM
     
macro NSL(nu,U,u,v) 
		(
		-2*nu*(D(u):D(v))
        + u#p*div(v)
        + div(u)*v#p
        - Conv(U,u,v)
        )
//EOM
        
macro Boundaryconditions(u1,u2,v1,v2,symmetry)
	           on(2,21,22,23,u1=0.0,u2=0.0) 
             + int1d(th,6)(u1*v1*1e30*(symmetry=="A")+u2*v2*1e30*(symmetry=="S")) 
//EOM 

//macro DeltaP(u1,u2,p)  (p(-10,0)-p(30,5))
//EOM

//macro DeltaP(u1,u2,p) ( 1/int2d(th,1)(y)*int2d(th,1)(p*y) - 1/int2d(th,3)(y*N.y)*int2d(th,1)(p*y*N.y) ) 
//EOM

macro DeltaP(u1,u2,p) ( int2d(th,1)(p*y) - int2d(th,3)(p*y*N.y) ) 
//EOM



macro QQ1(u)
    (
	int1d(th,91)(-(u#x*N.x+u#r*N.y)*2*pi*y)
	)
//EOM


macro EnergyNorm(u) sqrt( 2*pi* int2d(th)(abs(u#x)^2+abs(u#r)^2)*y )
// "Energy" norm for a real or complex field (factor 2 is because the whole domain is two halves)

macro EnergyNormCS(uc,us) sqrt( 2*pi*.5* int2d(th)(abs(uc#x)^2+abs(uc#r)^2+abs(us#x)^2+abs(us#r)^2)*y )
// "Energy" norm for a field defined by "cos" and "sin" components (factor 2 because half-domain ; factor .5 because of time-average)

real eps=1.e-7; 
       
       
real sigma1; // parameter sigma1 from Mantic-lugo et al ; should be zero for harmonic balance
//real A,dA ; // amplitude 


cout << " Choice of Re : " << endl;
cin >> Re; 
cout << " --> Re = " << Re << endl;
real nu=1/Re;

{
real Relu;
cout << "starting  from mean flow in file MeanFlow_guess.txt" << endl;
ifstream cbin(ffdatadir+"MeanFlow_guess.txt");
cbin >> ubx[] >> Relu;
if(Relu!=Re){ cout << "WARNING : Re not equal to that of the guess mean flow" << endl;};
//cout << "Drag force of guess mean flow : " << Drag(1/Re,ubx,uby,ubp) << endl;
}

//int check=exec("head -1 "+ffdatadir+"SelfConsistentMode_guess.txt");
//cout << "Check :"<<  check<< endl;

{
cout << "starting  from mode in file SelfConsistentMode_guess.txt" << endl;
ifstream cbin(ffdatadir+"SelfConsistentMode_guess.txt");

cbin >> u1x[] >> ReM >> sigmaM >> omegaM; 
cout << " 			omega of the starting field = " << omegaM<< endl;
//cout << "           Fy of the starting field : " << Lift(1/Re,u1x,u1r,u1p) << endl;
cout << "   		Energy-Norm (A) of starting field (complex macro) : " << EnergyNorm(u1)/sqrt(2) << endl;	
[u1cx,u1cr,u1cp] = [real(u1x), real(u1r), real(u1p)]; 
[u1sx,u1sr,u1sp] = [-imag(u1x), -imag(u1r), -imag(u1p)];  
cout << "   		Energy-Norm (A) of starting field (real macro) : " << EnergyNormCS(u1c,u1s) << endl << endl;
}

cout << " Guess for omega ? [-1 -> the one read in the guess file] " << endl; 
cin >> omega ;
if(omega==-1){omega = omegaM;};
cout << " -->  omegaguess = " << omega << endl;


cout << " Choice of sigma1 : [0 for Harmonic balance ; not zero for self-consistent] " << endl; 
cin >> sigma1 ;
cout << " --> sigma1 = " << sigma1 << endl;

string normalisation;
cout << " Normalisation choice ?  [none, Q or E]" << endl;
cin >> normalisation ;
cout << " --> " << normalisation << endl;


if(normalisation=="none")
	{ cout << " -> No normalization for the mode " << endl;}
	
else if(normalisation=="Q")
	{
		real Liftguess;
		cout << "Value of Lift force Q1 for renormalisation ?"<< endl;
		cin >> Liftguess;
		cout << "  -> Q1 = " << Liftguess  << endl;

		cout << "Q1 before renormalisation : " << QQ1(u1) << endl;
		complex modenorm = QQ1(u1);
		u1x[] = Liftguess/modenorm*u1x[]; // normalize so that lift is 0.5 and Cy 1
			cout << "Fy after renarmalisation : " << QQ1(u1) << endl;
		    cout << "   ### Energy-norm A = " << EnergyNorm(u1)/sqrt(2) << endl << endl;	
	
	}	
else if(normalisation=="E")
		{
		real Eguess;
		cout << "Value of ENERGY for renormalisation ?"<< endl;
		cin >> Eguess;
		cout << "  -> Energy = " << Eguess << endl;

		cout << "Energy-norm before renormalisation : " << EnergyNorm(u1)/sqrt(2) << endl;
		complex modenorm = QQ1(u1);
//		u1x[] = 1/modenorm*u1x[]; // first normalize so that imag part of lift is zero
		modenorm = EnergyNorm(u1)/sqrt(2);
		u1x[] = Eguess/modenorm*u1x[]; // then normalize so that Energy-norm is specified value
		cout << "Energy-norm after renarmalisation : " << EnergyNorm(u1)/sqrt(2) << endl;	
//		cout << "Cy after renarmalisation : " << Lift(1/Re,u1x,u1r,u1p) << endl;
		};

[u0x,u0r,u0p] = [ubx,ubr,ubp]; 
[u1cx,u1cr,u1cp] = [real(u1x), real(u1r), real(u1p)]; 
[u1sx,u1sr,u1sp] = [-imag(u1x), -imag(u1r), -imag(u1p)]; 
cout << "   		Energy-Norm (A) of starting field (real macro) : " << EnergyNormCS(u1c,u1s) << endl << endl;


////////////////////////////////////////////////////////////////////////////////
//
// In description, the blocks are called NxM, where N is the numering of the equation and M that of the unknown
// N = 1 steady NS for MEAN flow ; N=2 real part of linear equation for eigenmode ; N = 3 imaginary part; N = 4 phase 
// M = 1 MEAN-flow, M=2 eigenmode-real ; M = 3 eigenmode-imag ; M = 4 domega 
//
/////////////////////////////////////////////////////////////////////////////////




/////////////////////////////////////////////////////////////////////////////////
//////// varf for RHS 

////////////////////////////////////////////////////////////////////////////////////

cout << " defining operators for nonlinear term..." << endl;


varf NLOpF1([du0x,du0r,du0p],[vx,vr,vp]) =
// block 1 : NS for base flow
   int2d(th)( NS(nu,u0,v)*y )		
    +int2d(th)( (- .25*Conv(u1c,u1c,v) - .25*Conv(u1s,u1s,v))*y )
    + Boundaryconditions(du0x,du0r,vx,vr,"S") 
//    + int1d(th,6)(du0r*vr*1e30) 
//    + on(2,21,22,23,du0x=0.0,du0r=0.0) 
    	
 	;
 			
// block 2 : base-flow effect on real part of eigenmode			
varf NLOpF2([du1cx,du1cr,du1cp],[vx,vr,vp]) =
	 int2d(th)( NSL(nu,u0,u1c,v)*y )
	- int2d(th) ( sigma1 * (u1cx*vx+u1cr*vr)*y )
	- int2d(th) ( omega * (u1sx*vx+u1sr*vr)*y ) 
    + Boundaryconditions(du1cx,du1cr,vx,vr,"S")			
	;
	
// block 3 : base-flow effect on imag part of eigenmode			
varf NLOpF3([du1sx,du1sr,du1sp],[vx,vr,vp]) =
	 int2d(th)( NSL(nu,u0,u1s,v)*y  )              	
	- int2d(th) ( sigma1 * (u1sx*vx+u1sr*vr)*y )
	+ int2d(th) ( omega * (u1cx*vx+u1cr*vr)*y ) 
	+ Boundaryconditions(du1sx,du1sr,vx,vr,"S")		
	;

// blocks 4 : fix the phase with imag(lift)
// this is constructed directly when assemblying the matrix
cout << " defining operators for nonlinear term... done" << endl;

	



/////////////////////////////////////////////////////////////////////////////////
//////// varf for Linear part 

////////////////////////////////////////////////////////////////////////////////////

cout << " defining operators for linear term..." << endl;

// block 1-1 : NS for base flow
varf LinOp11([du0x,du0r,du0p],[vx,vr,vp]) =
   	int2d(th)( NSL(nu,u0,du0,v)*y )        
 	+ Boundaryconditions(du0x,du0r,vx,vr,"S");

// block 1-2 : nonlinear term (real parts)
varf LinOp12([du1cx,du1cr,du1cp],[vx,vr,vp]) =
      int2d(th)( -.5*Conv(u1c,du1c,v)*y );

// block 1-3 : nonlinear term (real parts)
varf LinOp13([du1sx,du1sr,du1sp],[vx,vr,vp]) =
      int2d(th)( -.5*Conv(u1s,du1s,v)*y );
 			

 			
// block 2-1 : base-flow effect on real part of eigenmode			
varf LinOp21([du0x,du0r,du0p],[vx,vr,vp]) =
	 int2d(th)( - Conv(du0,u1c,v)*y );
             
// block 2-2 : Stokes operator on real part of eigenmode             
varf LinOp22([du1cx,du1cr,du1cp],[vx,vr,vp]) =
             int2d(th)( NSL(nu,u0,du1c,v)*y )
            - int2d(th)( sigma1*(du1cx*vx+du1cr*vr)*y )
 			+ Boundaryconditions(du1cx,du1cr,vx,vr,"S");
 			
// block 2-3 : effect of omega on real part of equation for eigenmode
varf LinOp23([du1sx,du1sr,du1sp],[vx,vr,vp]) =
			 int2d(th)(-omega*(du1sx*vx+du1sr*vr)*y);

// block 2-4 : effect of domega on eigenmode-real equations
varf LinOp24([du0x,du0r,du0p],[vx,vr,vp])
	=  int2d(th)(-(u1sx*vx+u1sr*vr)*y); // this is to be multiplied by domega



// block 3-1 : base-flow effect on imag part of eigenmode			
varf LinOp31([du0x,du0r,du0p],[vx,vr,vp]) =
	 int2d(th)( - Conv(du0,u1s,v)*y );  
	             
// block 3-2 : effect of omega on imag part of equation for eigenmode
varf LinOp32([du1cx,du1cr,du1cp],[vx,vr,vp]) =
			 int2d(th)(omega*(du1cx*vx+du1cr*vr)*y);
			 			
// block 3-3 : Stokes operator on real part of eigenmode             
varf LinOp33([du1sx,du1sr,du1sp],[vx,vr,vp]) =
             int2d(th)( NSL(nu,u0,du1s,v)*y )
           - int2d(th) ( sigma1*(du1sx*vx+du1sr*vr)*y )
 		   + Boundaryconditions(du1sx,du1sr,vx,vr,"S");
 			;

// block 3-4 : effect of domega on eigenmode-imag equations
varf LinOp34([du0x,du0r,du0p],[vx,vr,vp])
	= int2d(th)((u1cx*vx+u1cr*vr)*y);


// bloc 4-3 : to fix the phase by imposing Lift(u1s) = 0
varf LinOp43([vx,vr,vp],[du1sx,du1sr,du1sp])	
	= QQ1(du1s);//DeltaP(du1sx,du1sr,du1sp); 
//	= Lift(1/Re,du1sx,du1sr,du1sp);


cout << " defining operators for linear term... done" << endl << endl;


real tol = 1e-6;

///// BEGINING OF NEWTON LOOP
real dXX = 1;
int iter=1;
while(abs(dXX)>tol&&abs(dXX)<1000&&iter<100)
	{ 
	cout << endl << "Newton loop step " << iter << endl;

	cout << "constructing rhs... " << endl;

	real[int] RHSF1(u0x[].n), RHSF2(u0x[].n), RHSF3(u0x[].n); 
	RHSF1 = NLOpF1(0,XXMh);
	RHSF2 = NLOpF2(0,XXMh);
	RHSF3 = NLOpF3(0,XXMh);	
	real RHSF4 = QQ1(u1s);//DeltaP(u1sx,u1sr,u1sp); 
	//= Lift(1/Re,u1sx,u1sr,u1sp); 	
	
	real[int] RHS = [RHSF1, RHSF2, RHSF3, RHSF4];		
//	cout <<  " max RHS : " << RHSF1.l2 <<  ", " << RHSF2.l2 <<  ", " << RHSF3.l2 <<  ", " << RHSF4 << endl;


	
	cout << " Assembling matrix..." << endl;
	
matrix matLinOp11 = LinOp11(XXMh,XXMh);
matrix matLinOp12 = LinOp12(XXMh,XXMh);
matrix matLinOp13 = LinOp13(XXMh,XXMh);

matrix matLinOp21 = LinOp21(XXMh,XXMh);
matrix matLinOp22 = LinOp22(XXMh,XXMh);
matrix matLinOp23 = LinOp23(XXMh,XXMh);
real[int] matLinOp24 = LinOp24(0,XXMh);


matrix matLinOp31 = LinOp31(XXMh,XXMh);
matrix matLinOp32 = LinOp32(XXMh,XXMh);
matrix matLinOp33 = LinOp33(XXMh,XXMh);
real[int] matLinOp34 = LinOp34(0,XXMh);

real[int] matLinOp43 = LinOp43(0,XXMh);

matrix LinOp = [	[matLinOp11, matLinOp12, matLinOp13, 	0 ],
					[matLinOp21,matLinOp22,  matLinOp23,	matLinOp24 ],
					[matLinOp31,matLinOp32, matLinOp33,		matLinOp34 ],
					[	0	   ,0 	, 		matLinOp43',	0 		 ]
			   ];
				
cout << " Assembling matrix... done" << endl;


cout << " LU-ing matrix..." << endl;
set(LinOp,solver=sparsesolver);
cout << " LU-ing matrix... done" << endl;

	
	cout << " solving BIG problem..." << endl;
	real[int] dX(LinOp.n);
	dX = LinOp^-1*RHS;
	[du0x[], du1cx[], du1sx[], domega] = dX;
	
	u0x[] -= du0x[];
	u1cx[] -= du1cx[];
	u1sx[] -= du1sx[];
	omega -= domega;




	cout << endl << "   ### ||du0||, ||du1,r||, ||du1,i||, domega : " << du0x[].l2 
			<< " " << du1cx[].l2 << " " << du1sx[].l2 << " " << -domega << endl;
	dXX = du0x[].l2+du1cx[].l2+du1sx[].l2+abs(domega);
	
	cout << "   ### ||u0||, ||u1,r||, ||u1,i|| : " << u0x[].l2 
			<< " " << u1cx[].l2 << " " << u1sx[].l2  << endl;

	
	cout <<  "   ### omega = " << omega << endl;
	cout << "   ### Energy-norm of perturbation A = " << EnergyNormCS(u1c,u1s)  << endl;	
//	cout << " 	### Fy = " << Lift(1/Re,u1cx,u1cr,u1cp) << endl ; 
	IFMACRO(FREEFEMPLOTS,YES)
	plot(u0x,cmm="mean flow approximation iteration "+iter, fill=1,value=1,wait=0);
	ENDIFMACRO

	iter++;
	if(u1cx[].l2<1e-6){dXX = 1e30; };
	};
	
	if(abs(dXX)>1000) 
	{
	SFWriteBaseFlow(ffdatadir+"MeanFlow.ff2m",ub,"MeanFlow",-1);
	SFWriteMode(ffdatadir+"SelfConsistentMode.ff2m",u1,1i*omega+sigma1,1i*omegaM+sigma1,0,"HarmonicMode",-1);
	cout << endl << endl << "#####################################################################@" << endl << endl;
	cout << " Newton Loop DIVERGED " << endl;	
	}
	
	else
	{
	cout << endl << endl << "#####################################################################@" << endl << endl
	     << "HB MODEL : Newton Loop converged in " << iter << " iterations " << endl;
	cout << " >>> Omegac = " << omega << endl;	
	cout << " >>> Energy of perturbation A = " << EnergyNormCS(u1c,u1s)  << endl;	
//	cout << " >>> Fy = " << Lift(1/Re,u1cx,u1cr,u1cp) << endl ; 
	
/// GENERATES OUTPUT FILES

{
	ofstream file(ffdatadir+"MeanFlow.txt");
	file << u0x[]; 
	file << endl << endl << Re;
}; 

SFWriteBaseFlow(ffdatadir+"MeanFlow.ff2m",u0,"MeanFlow",iter);

    [u1x,u1r,u1phi,u1p] = [u1cx-1i*u1sx,u1cr-1i*u1sr,0,u1cp-1i*u1sp]; 

{
 	ofstream file(ffdatadir+"SelfConsistentMode.txt");
   	file << u1x[] ;	
   	file << endl << endl << Re << endl
   	<< endl << sigma1 << endl << omega  << endl;
};


SFWriteMode(ffdatadir+"SelfConsistentMode.ff2m",u1,1i*omega+sigma1,1i*omega+sigma1,0,"HarmonicMode",iter);

if(copyfile=="yes")
{
cout << " COPYING Output into guess for next calculation..." << endl; 
{
	ofstream file(ffdatadir+"MeanFlow_guess.txt");
	file << u0x[]; 
	file << endl << endl << Re;
};
{
 	ofstream file(ffdatadir+"SelfConsistentMode_guess.txt");
   	file << u1x[] ;	
   	file << endl << endl << Re << endl
   	<< endl << sigma1 << endl << omega  << endl;
};
};

	};
	
// END PROGRAM

